* sharedSubs
  | client client:recipient
  | shared alias view
  remote = "server"
  asCells = false

* remote|subscription
  | sharedSubs remote view recipient alias asCells

* remote|view
  | remote|subscription remote alias:view

* remote|field
  | remote|subscription remote alias:view
  | field view field ix

* remote
  | sharedSubs remote

* remote
  | remote|subscription remote

* manuallyShared
  ~ alias view

* shared
  | manuallyShared alias view

* manuallySubscribe
  ~ remote view alias asCells recipient

* remote|subscription
  | manuallySubscribe remote alias view asCells recipient

* event
  | rawEvent client eid label key value
  | !removedEvent client eid

;;; FSM

;; Initial Static data
* fsm_initialStates
  ~ id machine state

* fsm_initialTypedStates
  ~ id type machine state

* fsm_initialMachines
  ~ id transition from to

* fsm_initialTypedMachines
  ~ type id transition from to

* fsm_initialTransitions
  ~ id transition machine

* fsm_typedMachines
  | fsm_initialTypedMachines type id transition from to

* fsm_machines
  | fsm_initialMachines id transition from to

* fsm_machines
  | fsm_mungedTypedMachines munged:id transition from to

* fsm_typedStates
  | fsm_initialTypedStates id type machine state

* fsm_states
  | fsm_initialStates id machine state

* fsm_states
  | fsm_mungedTypedStates id munged:machine state

* fsm_states
  | fsm_machines id:machine
  | fsm_transitions machine id state

* fsm_currentState
  | fsm_machines id:machine
  > fsm_states machine | state = maxBy(@fsm_states.state, @fsm_states.id)

* fsm_potentialTransitions
  | fsm_initialTransitions id transition machine

* fsm_transitions
  | fsm_machines id:machine transition from:prev to:state
  | fsm_potentialTransitions id transition machine
  > fsm_states machine | prev = lastBefore(@fsm_states.state, @fsm_states.id, @id, "")

; Internal munging
* fsm_mungedTypedMachines
  | fsm_typedMachines type id transition from to
  munged = @type + "-" + @id

* fsm_mungedTypedStates
  | fsm_typedStates id type machine state
  munged = @type + "-" + @machine
