* timer
 ~ id event rate
 + 3 "minutes" 6000

* clients
  ~  id name
  + 1 "Jen"
  + 2 "Jeff"
  + 3 "Bill"

* projects
  ~ id client name
  + 1 1 "Jen's Cafe"
  + 2 3 "Bill's Homepage"

* rawTimes
  ;~ project start end duration
  | projects id:project
  | fsm_states machine:project state="working" id:startId
  > fsm_states machine:project state="paused" | endId = firstAfter(@fsm_states.id, @fsm_states.id, @startId, 999999999999)
  > event label="minutes" | startMS = firstAfter(@event.value, @event.eid, @startId, 0)
  > event label="minutes" | endMS = lastBefore(@event.value, @event.eid, @endId, 0)
  durationMS = @endMS - @startMS
  start = hours(@startMS) + ":" + minutes(@startMS)
  end = hours(@endMS) + ":" + minutes(@endMS)
  duration = +(@durationMS / 60000).toFixed(1)


* times
  | rawTimes project start end duration


* fsm_machines
  | projects id
  transition = "toggle working"
  from = "paused"
  to = "working"

* fsm_machines
  | projects id
  transition = "toggle working"
  from = "working"
  to = "paused"

* fsm_states
  | projects id:machine
  id = -1
  state = "paused"

* fsm_potentialTransitions
  | fsm_machines id:machine
  | event eid:id label:transition key:machine

* _view fsm_currentState
  | fsm_currentState machine state



;;; UI

* ui root
  ["div" {"id" "root"}
    ;["button" {"click" "click" "key" "button" "doubleClick" "dblClick"} "hi"]
    ["div" {"id" "trackers" "class" "trackers"}]
  ]

* ui project tracker
  | projects id client name
  | clients id:client name:clientName
  | fsm_currentState machine:id state:workingState
  > times project:id | totalElapsed = sum(@times.duration)

  workingToggle = @workingState == "paused" ? "start" : "pause"

  ["div" {"id" id "parent" "trackers" "class" "project-tracker"}
    ["h3" name]
    " for "
    ["span" {"class" "client-name"} clientName]

    ["div" "Elapsed time: " totalElapsed " minutes"]

    ["div" {"class" "controls"}
      ["button" {"click" "toggle working"
                 "key" id}
                 workingToggle]
    ]
  ]
