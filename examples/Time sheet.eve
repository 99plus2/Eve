
;* aaaa
;  | event eid label key value

* timer
 ~ id event rate
 + 3 "minutes" 6000

* clients
  ~  id name
  + 1 "Jen"
  + 2 "Jeff"
  + 3 "Bill"

* initialProjects
  ~ id client name
  + 1 1 "Jen's Cafe"
  + 2 3 "Bill's Homepage"

* projects
  | initialProjects id client name
  version = 0

* currentProjects
  | projects id client name version
  > projects id | version = maxBy(@projects.version, @projects.version, 0)

;; Calculate times
* rawTimes
  | currentProjects id:project
  | fsm_states type="timer" instance:project state="working" id:startId
  ;; Infinity isn't working?
  > fsm_states type="timer" instance:project state="paused" | endId = firstAfter(@fsm_states.id, @fsm_states.id, @startId, 999999999999)
  | eventTime tick:startId time:startMS
  > event label="minutes" | endMS = lastBefore(@event.value, @event.eid, @endId, 0)
  durationMS = @endMS - @startMS
  start = hours(@startMS) + ":" + minutes(@startMS)
  end = hours(@endMS) + ":" + minutes(@endMS)
  duration = +(@durationMS / 60000).toFixed(1)

* times
  | rawTimes project start end duration


;; Add timer FSM
* fsm_transitions
  | currentProjects id:instance
  type = "timer"
  transition = "toggle working"
  from = "paused"
  to = "working"

* fsm_transitions
  | currentProjects id:instance
  type = "timer"
  transition = "toggle working"
  from = "working"
  to = "paused"

* fsm_states
  | currentProjects id:instance
  type = "timer"
  id = -1
  state = "paused"

;; Add title editing FSM
* fsm_transitions
  | currentProjects id:instance
  type = "title"
  transition = "edit title"
  from = "display"
  to = "editing"

* fsm_transitions
  | currentProjects id:instance
  type = "title"
  transition = "save title"
  from = "editing"
  to = "display"

* fsm_states
  | currentProjects id:instance
  type = "title"
  id = -1
  state = "display"


;; Shovel potential transactions into the system
* potentialTransitions
  | fsm_transitions type instance transition
  | event eid:id label:transition key
  ? @key ==  @type + "|||" + @instance

* fsm_potentialTransitions
  ;; @TODO: fixme
  | potentialTransitions type instance id transition


;; Keep an eye on current state
* view fsm_currentState
  | fsm_currentState type instance state

* view fsm_states
  | fsm_states type instance id state


;;; UI

* ui root
  ["div" {"id" "root" "class" "time-sheet-example"}
    ["h2" "Time Sheets"]
    ["div" {"id" "trackers" "class" "trackers"}]
  ]

* ui project tracker
  | currentProjects id client name
  | clients id:client name:clientName
  | fsm_currentState type="timer" instance:id state:workingState
  > times project:id | totalElapsed = sum(@times.duration).toFixed(1)
  timerFSM = "timer|||" + @id
  workingToggle = @workingState == "paused" ? "start" : "pause"

  ["div" {"id" id "parent" "trackers" "class" "project-tracker"}
    " for "
    ["span" {"class" "client-name"} clientName]

    ["span" {"class" "elapsed"} totalElapsed " minutes"]

    ["div" {"class" "controls"}
      ["button" {"click" "toggle working"
                 "key" timerFSM}
                 workingToggle]
    ]
  ]

* ui title (display)
  | currentProjects id client name
  | fsm_currentState type instance:id state
  titleFSM = @type + "|||" + @id
  ? @state == "display"
  ["h3" {"id" titleFSM "parent" id "ix" 0
         "click" "edit title"
         "key" titleFSM} name]

* ui title (editing)
  | currentProjects id client name
  | fsm_currentState type instance:id state
  titleFSM = @type + "|||" + @id
  ? @state == "editing"
  ["input" {"id" titleFSM "parent" id "ix" 0
         "class" title
         "blur" "save title"
         "input" "update title"
         "key" titleFSM
         "value" name}]
