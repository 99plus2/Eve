
;* aaaa
;  | event eid label key value

* timer
 ~ id event rate
 + 3 "minutes" 6000

* clients
  ~  id name
  + 1 "Jen"
  + 2 "Jeff"
  + 3 "Bill"

* initialProjects
  ~ id client name
  + 1 1 "Jen's Cafe"
  + 2 3 "Bill's Homepage"

* projects
  | initialProjects id client name
  version = 0

* currentProjects
  | projects id client name version
  > projects id | version = maxBy(@projects.version, @projects.version, 0)

;; Calculate times
* rawTimes
  | currentProjects id:project
  | fsm_states type="timer" instance:project state="working" id:startId
  ;; Infinity isn't working?
  > fsm_states type="timer" instance:project state="paused" | endId = firstAfter(@fsm_states.id, @fsm_states.id, @startId, 999999999999)
  | eventTime tick:startId time:startMS
  > event label="minutes" | endMS = lastBefore(@event.value, @event.eid, @endId, 0)
  durationMS = @endMS - @startMS
  start = hours(@startMS) + ":" + minutes(@startMS)
  end = hours(@endMS) + ":" + minutes(@endMS)
  duration = +(@durationMS / 60000).toFixed(1)

* times
  | rawTimes project start end duration


;; Add FSMs
* transitions
  ~ type transition from to
  + "timer" "toggle working" "paused" "working"
  + "timer" "toggle working" "working" "paused"
  + "title" "edit title" "display" "editing"
  + "title" "save title" "editing" "display"

* startingStates
  ~ type id state
  + "timer" -1 "paused"
  + "title" -1 "display"

* fsm_transitions
  | currentProjects id:instance
  | transitions type transition from to

* fsm_states
  | currentProjects id:instance
  | startingStates type id state


;; Shovel potential transactions into the system
* potentialTransitions
  | fsm_transitions type instance transition
  | event eid:id label:transition key
  ? @key ==  @type + "|||" + @instance

* fsm_potentialTransitions
  ;; @TODO: fixme
  | potentialTransitions type instance id transition


;; Keep an eye on current state
* view fsm_currentState
  | fsm_currentState type instance state

* view fsm_states
  | fsm_states type instance id state


;;; UI

* ui root
  ["div" {"id" "root" "class" "time-sheet-example"}
    ["h2" "Time Sheets"]
    ["div" {"id" "trackers" "class" "trackers"}]
  ]

* ui project tracker
  | currentProjects id client name
  | clients id:client name:clientName
  | fsm_currentState type="timer" instance:id state:workingState
  > times project:id | totalElapsed = sum(@times.duration).toFixed(1)
  timerFSM = "timer|||" + @id
  workingToggle = @workingState == "paused" ? "start" : "pause"

  ["div" {"id" id "parent" "trackers" "class" "project-tracker"}
    " for "
    ["span" {"class" "client-name"} clientName]

    ["span" {"class" "elapsed"} totalElapsed " minutes"]

    ["div" {"class" "controls"}
      ["button" {"click" "toggle working"
                 "key" timerFSM}
                 workingToggle]
    ]
  ]

* ui title (display)
  | currentProjects id client name
  | fsm_currentState type instance:id state
  titleFSM = @type + "|||" + @id
  ? @state == "display"
  ["h3" {"id" titleFSM "parent" id "ix" 0
         "click" "edit title"
         "key" titleFSM} name]

* ui title (editing)
  | currentProjects id client name
  | fsm_currentState type instance:id state
  titleFSM = @type + "|||" + @id
  ? @state == "editing"
  ["input" {"id" titleFSM "parent" id "ix" 0
         "class" title
         "autofocus" true
         "blur" "save title"
         "input" "update title"
         "key" titleFSM
         "value" name}]
